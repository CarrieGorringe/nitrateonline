<SCRIPT LANGUAGE="VBScript" RUNAT=Server>
Sub Application_OnStart

	DIM fso, file
	DIM dateFile
	DIM menuFile, menuStr, bodyStart, bodyLen

	CONST ForReading = 1
	CONST szBodyStart = "<body>"
	CONST szBodyEnd = "</body>"

	set fso = CreateObject ("Scripting.FileSystemObject")

	REM read in the issue date
	set file = fso.OpenTextFile (Server.MapPath("/issdate.txt"), ForReading)
	dateFile = file.ReadAll
	file.Close
	
	REM Read in the menu file into memory
	set file = fso.OpenTextFile (Server.MapPath ("/issmenu.html"), ForReading)
	menuFile = file.ReadAll
	file.Close
	
	REM Find the "body" of the menu file
	bodyStart = InStr (menuFile, szBodyStart) + Len (szBodyStart)
	bodyLen = InStr (menuFile, szBodyEnd)- bodyStart
	
	menuStr = Mid (menuFile, bodyStart, bodyLen)


	Application ("IssueDate") = dateFile
	Application ("MenuBar") = menuStr
	
End Sub
</SCRIPT>
